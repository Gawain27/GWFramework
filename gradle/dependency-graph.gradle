/* =========================================================================
* Level-based  implementation()  wiring between included modules
* ========================================================================= */
Map<String, Integer> projectLevels = [:]
ext.projectLevels = projectLevels      // share with i18n script

subprojects { Project sp ->
  if (!includedProjectNames.contains(sp.name)) return
  sp.afterEvaluate {
    Integer lvl = (sp.findProperty('projectLevel') ?: 0).toInteger()
    projectLevels[sp.name] = lvl
  }
}

/* wire after all projects are evaluated */
gradle.projectsEvaluated {
  subprojects { Project sp ->
    if (!includedProjectNames.contains(sp.name)) return
    int myLevel = projectLevels[sp.name] ?: 0
    includedProjectNames.each { String other ->
      if (other == sp.name) return
      if (myLevel >= (projectLevels[other] ?: 0)) {
        sp.dependencies { implementation project(":$other") }
      }
    }
  }
}
